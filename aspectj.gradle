
/**
 * Copied from http://www.breskeby.com/2010/02/using-gradle-with-aspectj/
 * It didn't work straight off for me, so below are just the modifications I
 * needed for my own projects.
 */

apply plugin: 'java'

configurations {
	ajc
	aspects
	ajInpath
}

dependencies {
	ajc 'org.aspectj:aspectjtools:1.7.0'
	compile 'org.aspectj:aspectjrt:1.7.0'
}

task compileJava(dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME, overwrite: true) {
	dependsOn configurations.ajc.getTaskDependencyFromProjectDependency(true, 'compileJava')

	doLast{
		ant.taskdef(
			resource: 'org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties',
			classpath: configurations.ajc.asPath)
		ant.iajc(
			source: sourceCompatibility,
			target: targetCompatibility,
			destDir: sourceSets.main.output.classesDir.absolutePath,
			maxmem: '512m',
			fork: 'true', 
			aspectPath: configurations.aspects.asPath,
			inpath: configurations.ajInpath.asPath,
			sourceRootCopyFilter: '**/.svn/*,**/*.java',
			classpath: configurations.compile.asPath) {	
		
			sourceroots {
				sourceSets.main.java.srcDirs.each { dir ->
					if (dir.exists()) {
						pathelement(location: dir.absolutePath)
					}
				}
			}
		}
	}
}
